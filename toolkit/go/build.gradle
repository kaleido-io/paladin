/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.LineContains

ext {
    goFiles = fileTree(".") {
        include "pkg/**/*.go"
        include "domainstarter/**/*.go"
    }
    targetCoverage = 99.9
    maxCoverageBarGap = 1
    coverageExcludedPackages = [
        'github.com/LF-Decentralized-Trust-labs/paladin/toolkit/pkg/prototk',
        'github.com/LF-Decentralized-Trust-labs/paladin/toolkit/pkg/reference',
    ]
}

configurations {
    // Resolvable configurations
    protoDefinitions {
        canBeConsumed = false
        canBeResolved = true
    }

    // Consumable configurations
    goSource {
        canBeConsumed = true
        canBeResolved = false
    }
    libstarter {
        canBeConsumed = true
        canBeResolved = false
    }
}

dependencies {
    protoDefinitions project(path: ":toolkit:proto", configuration: "protoDefinitions")
}

task protoc(type: ProtoCompile, dependsOn: [":installProtocGenGo", ":installProtocGenGoRPC"]) {
    protocPath "bin"
    protoPath configurations.protoDefinitions.asPath
    protoFiles fileTree(configurations.protoDefinitions.asPath) {
        include "**/*.proto"
    }
    plugins {
        go {
            out "${projectDir}/pkg/prototk"
            opt 'paths=source_relative'
            fileTree(configurations.protoDefinitions.asPath) {
               include "*.proto"
            }.each { File file ->
                opt "M${file.name}=pkg/prototk"
            }
        }
        go_grpc {
            out "${projectDir}/pkg/prototk"
            opt 'paths=source_relative'
            fileTree(configurations.protoDefinitions.asPath) {
               include "*.proto"
            }.each { File file ->
                opt "M${file.name}=pkg/prototk"
            }
        }
    }
}

task clean(type: Delete, dependsOn: ':testinfra:stopTestInfra') {
    delete "${buildDir}"

    delete fileTree("bin") {
        exclude "README.md", ".gitignore"
    }
    delete fileTree(".") {
        include "**/*.pb.go"
    }
    delete 'coverage'
    delete 'componenttest/abis'
}

task goGet(type:Exec, dependsOn: protoc) {
    workingDir '.'

    inputs.files('go.mod')
    inputs.files(goFiles)
    outputs.files('go.sum')

    executable 'go'
    args 'get'
    args './pkg/...'
}

task lint(type: Exec, dependsOn:[protoc, ":installGolangCILint"]) {
    workingDir '.'

    helpers.lockResource(it, "lint.lock")
    inputs.files(goFiles)
    environment 'GOGC', '20'

    executable "golangci-lint"
    args 'run'
    args '-v'
    args '--color=always'
    args '--timeout', '5m'
    args '--tests=false'
}

task generateDocs(type: Exec) {

    inputs.files(getProject().goFiles)
    outputs.dir('coverage')

    workingDir '.'
    executable 'go'
    args 'test'
    args './pkg/reference'
    args '-tags=reference'
    if (project.findProperty('verboseTests') == 'true') {
        args '-v'
    }
    
}

task test(type: Exec, dependsOn: [goGet]) {
    inputs.files(getProject().goFiles)
    outputs.dir('coverage')

    workingDir '.'
    executable 'go'
    args 'test'
    args './pkg/...'
    args '-cover'
    args '-covermode=atomic'
    args '-timeout=30s'
    if (project.findProperty('verboseTests') == 'true') {
        args '-v'
    }
    args "-test.gocoverdir=${getProject().projectDir}/coverage"
}

task buildCoverageTxt(type: Exec, dependsOn: [
        protoc,
        lint,
        test,
    ]) {
    inputs.files(fileTree(project.mkdir('coverage')) {
        include "covcounters.*"
        include "covmeta.*"
    })
    outputs.files('coverage/coverage_unfiltered.txt')
    executable 'go'
    args 'tool', 'covdata', 'textfmt'
    args '-i', "${projectDir}/coverage"
    args '-o', "${projectDir}/coverage/coverage_unfiltered.txt"

}

task aggregateCoverage(type: Copy, dependsOn: buildCoverageTxt) {
    from 'coverage/coverage_unfiltered.txt'
    into 'coverage'
    outputs.files('coverage/coverage.txt')
    eachFile { fcd ->
        fcd.setRelativePath(fcd.relativePath.replaceLastName('coverage.txt'))
    }
    filter(LineContains, negate: true, contains: getProject().ext.coverageExcludedPackages, matchAny: true) 
}

task checkCoverage(type: GoCheckCoverage, dependsOn: [aggregateCoverage]) {
    coverageFile('coverage/coverage.txt')
    target = targetCoverage
    maxGap = maxCoverageBarGap
}

task buildStarterLibrary(type:GoLib, dependsOn:[goGet]) {
    baseName "starter"
    sources goFiles
    mainFile './domainstarter'
}

task build {
    dependsOn lint
    dependsOn test
    dependsOn checkCoverage
    dependsOn buildStarterLibrary
}

task assemble {
    dependsOn buildStarterLibrary
}

dependencies {
    libstarter files(buildStarterLibrary)
    goSource files(goFiles, protoc)
}
